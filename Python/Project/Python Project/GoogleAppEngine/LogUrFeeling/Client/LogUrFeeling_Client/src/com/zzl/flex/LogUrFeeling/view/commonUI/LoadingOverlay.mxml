<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
	width="100%" height="100%"
	creationComplete="OnInit()">
	
	<mx:Script>
		<![CDATA[
			import mx.core.UIComponent;
			import com.zzl.flex.LogUrFeeling.Model.MainModelLocator;
			
			private var _mainModelLocator:MainModelLocator = MainModelLocator.inst;
			[Bindable]
			private var _percent:int = 0;
			private var _loadingBar:CustomPreloader;
			private var _useProgress:Boolean = true;
			
			public function set useProgress(val:Boolean):void
			{
				_useProgress = val;
			}
			
			private function OnInit():void
			{
				this.addEventListener(Event.ADDED, OnSelfAdded, false, 0, true);
				this.addEventListener(Event.REMOVED, OnSelfRemoved, false, 0, true);
				_mainModelLocator.addEventListener(MainModelLocator.E_MODULE_LOAD_PROGRESS_UPDATE, OnProgress, false, 0, true);
				
				CreateLoadingAnim();
			}
			
			private function OnSelfAdded(e:Event):void
			{
				var blur:BlurFilter = new BlurFilter;
				blur.blurX = 10;
				blur.blurY = 10;
				blur.quality = 2;
					
				var mainFrame:UIComponent = _mainModelLocator.mainFrame;
				mainFrame.filters = [blur];
				
				_loadingBar.visible = _useProgress;
				loadingTxt.visible = !_useProgress;
			}
			
			private function OnSelfRemoved(e:Event):void
			{
				var mainFrame:UIComponent = _mainModelLocator.mainFrame;
				mainFrame.filters = [];
			}
			
			private function CreateLoadingAnim():void
			{
				_loadingBar = new CustomPreloader;
				this.rawChildren.addChild(_loadingBar);
				_loadingBar.gotoAndStop(1);
				_loadingBar.x = this.width * 0.5 - 135;
				_loadingBar.y = this.height * 0.5 - 34;
			}
			
			private function OnProgress(e:Event):void
			{
				_percent = _mainModelLocator.moduleLoadProgress;
				if (_loadingBar != null)
				{
					_loadingBar.percent.text = _percent.toString() + "%";
					_loadingBar.gotoAndStop(_percent);
				}
			}
			
		]]>
	</mx:Script>
	
	<mx:Label id="loadingTxt" x="{(this.width  - loadingTxt.width) * 0.5}" y="{(this.height - loadingTxt.height) * 0.5}"
			text="Loading..." color="#FFFFFF" fontSize="36"/>
	
</mx:Canvas>
